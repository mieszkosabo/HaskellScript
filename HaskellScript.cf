-- programs ------------------------------------------------

entrypoints Program ;

Program.   Program ::= [Stmt] ;

token Udent (upper (letter | digit | '_')*) ;

separator Ident "," ;

-- statements ----------------------------------------------

separator  Stmt "" ;

Block.     Block ::= "{" [Stmt] "}" ;

Decl.      Stmt ::= "const" Ident "=" Expr ";" ;

FunDecl.   Stmt ::= Ident "::"  [Type] "const" Ident "=" Expr ";" ;

DataDecl.  Stmt ::= "data" Udent "(" [Type] ")" "=" "{" [Constructor]  "}" ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" Block ;

CondElse.  Stmt ::= "if" "(" Expr ")" Block "else" Block ;

Ret.       Stmt ::= "return" Expr ";" ;

VoidRet.   Stmt ::= "return;" ;

Print.     Stmt ::= "_print" "(" [Expr] ")" ";" ;

Match.     Stmt ::= "match" Ident ":" [Case] ;

SExp.      Stmt ::= Expr ";" ;

-- Cases

Case.    Case ::= "case" Expr Block ;

separator Case "" ;
-- Types ---------------------------------------------------

Int.       Type ::= "Int" ;

Str.       Type ::= "String" ;

Bool.      Type ::= "Bool" ;

Void.      Type ::= "Void" ;

FunT.      Type ::= "(" [Type] ")" ;

ListT.     Type ::= "[" Type "]" ;

WildcardT. Type ::= Ident ;

DataType.  Type ::= Udent "(" [Type] ")";

separator Type "->" ;

-- constructor

TypeArg.    TypeArg ::= Type ;
separator TypeArg "," ;

Constructor.  Constructor ::= Udent "(" [TypeArg] ")";
separator nonempty Constructor "|" ;

-- Expressions ---------------------------------------------

EVar.      Expr8 ::= Ident ;

EConstr.   Expr8 ::= Udent ;

ELitInt.   Expr8 ::= Integer ;

EString.   Expr8 ::= String ;

ELitTrue.  Expr8 ::= "true" ;

ELitFalse. Expr8 ::= "false" ;

Ternary.   Expr ::= Expr1 "?" Expr1 ":" Expr1 ;

LongLambda. Expr7 ::= "\\" [Ident] "=>" Block ;

ConciseLambda. Expr7 ::= "\\" [Ident] "=>" Expr8 ;

Spread.     Expr ::= "..." Expr1 ;

ListExpr.  Expr7 ::= "[" [Expr] "]" ;

-- DataExpr. Expr6 ::= Udent [Type] ;  

EApp.      Expr6 ::= Expr7 "(" [Expr] ")";

Neg.       Expr5 ::= "-" Expr6 ;

Not.       Expr5 ::= "not" Expr6 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 "and" Expr1 ;

EOr.       Expr ::= Expr1 "or" Expr ;

coercions  Expr 8 ;

separator Expr "," ;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;

Minus.     AddOp ::= "-" ;

Times.     MulOp ::= "*" ;

Div.       MulOp ::= "/" ;

Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;

LE.        RelOp ::= "<=" ;

GTH.       RelOp ::= ">" ;

GE.        RelOp ::= ">=" ;

EQU.       RelOp ::= "==" ;

NE.        RelOp ::= "!=" ;

-- comments ------------------------------------------------

comment    "//" ;

comment    "/*" "*/" ;