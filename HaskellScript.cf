-- programs ------------------------------------------------

entrypoints Program ;

Program.   Program ::= [Stmt] ;

separator Ident "," ;

-- statements ----------------------------------------------

separator  Stmt "" ;

Block.     Block ::= "{" [Stmt] "}" ;

Decl.      Stmt ::= "const" Ident "=" Expr ";" ;

FunDecl.   Stmt ::= Ident "::"  [Type] "const" Ident "=" Expr ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" Block ;

CondElse.  Stmt ::= "if" "(" Expr ")" Block "else" Block ;

Ret.       Stmt ::= "return" Expr ";" ;

VoidRet.   Stmt ::= "return;" ;

Print.     Stmt ::= "print" "(" [Expr] ")" ";" ;

SExp.      Stmt ::= Expr ";" ;

-- Types ---------------------------------------------------

Int.       Type ::= "Int" ;

Str.       Type ::= "String" ;

Bool.      Type ::= "Bool" ;

Void.      Type ::= "Void" ;

FunT.      Type ::= "(" [Type] ")" ;

ListT.     Type ::= "[" Type "]" ;

separator Type "->" ;

-- Expressions ---------------------------------------------

EVar.      Expr8 ::= Ident ;

ELitInt.   Expr8 ::= Integer ;

EString.   Expr8 ::= String ;

ELitTrue.  Expr8 ::= "true" ;

ELitFalse. Expr8 ::= "false" ;

Ternary.   Expr ::= Expr1 "?" Expr1 ":" Expr1 ;

LongLambda. Expr7 ::= "\\" [Ident] "=>" Block ;

ConciseLambda. Expr7 ::= "\\" [Ident] "=>" Expr8 ;

-- ListExpr.  Expr6 ::= "[" [Expr] "]" ;

EApp.      Expr6 ::= Expr7 "(" [Expr] ")";

Neg.       Expr5 ::= "-" Expr6 ;

Not.       Expr5 ::= "not" Expr6 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 "and" Expr1 ;

EOr.       Expr ::= Expr1 "or" Expr ;

coercions  Expr 8 ;

separator Expr "," ;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;

Minus.     AddOp ::= "-" ;

Times.     MulOp ::= "*" ;

Div.       MulOp ::= "/" ;

Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;

LE.        RelOp ::= "<=" ;

GTH.       RelOp ::= ">" ;

GE.        RelOp ::= ">=" ;

EQU.       RelOp ::= "==" ;

NE.        RelOp ::= "!=" ;

-- comments ------------------------------------------------

comment    "//" ;

comment    "/*" "*/" ;